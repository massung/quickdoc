;;;; QuickDoc Markup for LispWorks
;;;;
;;;; Copyright (c) 2014 by Jeffrey Massung
;;;;
;;;; This file is provided to you under the Apache License,
;;;; Version 2.0 (the "License"); you may not use this file
;;;; except in compliance with the License.  You may obtain
;;;; a copy of the License at
;;;;
;;;;    http://www.apache.org/licenses/LICENSE-2.0
;;;;
;;;; Unless required by applicable law or agreed to in writing,
;;;; software distributed under the License is distributed on an
;;;; "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
;;;; KIND, either express or implied.  See the License for the
;;;; specific language governing permissions and limitations
;;;; under the License.
;;;;

(in-package :quickdoc)

(deflexer span-lexer

  ;; escaped characters
  ("\\(.)"            (values :text $1))

  ;; teletype formatting
  ("`"                (push-lexer 'tt-lexer :+tt))

  ;; special symbols
  ("%(tm%)"           (values :text #\u+2122))
  ("%(R%)"            (values :text #\u+00ae))
  ("%(C%)"            (values :text #\u+00a9))
  ("%(1/4%)"          (values :text #\u+00bc))
  ("%(1/2%)"          (values :text #\u+00bd))
  ("%(3/4%)"          (values :text #\u+00be))
  ("%(o%)"            (values :text #\u+00b0))
  ("%(%+/%-%)"        (values :text #\u+00b1))

  ;; emphasis
  ("%*%*"             (values :strong))
  ("__"               (values :em))

  ;; links
  ("%[%["             (push-lexer 'link-lexer :+link))

  ;; non-terminal characters
  (".[^%[%(%*_`]*"    (values :text $$)))

(deflexer tt-lexer

  ;; end of stream or teletype
  ("$"                (pop-lexer :-tt))
  ("`"                (pop-lexer :-tt))

  ;; escaped characters
  ("\\(.)"            (values :chars $1))

  ;; everything else is just characters
  (".[^`]*"           (values :chars $$)))

(deflexer link-lexer
          
  ;; end of stream, line, or link
  ("$"                (pop-lexer :-link))
  ("%]%]"             (pop-lexer :-link))

  ;; alternate text
  ("|"                (swap-lexer 'alt-lexer :alt))
  
  ;; everything else is the link
  (".[^|%]]*"         (values :url $$)))

(deflexer alt-lexer

  ;; end of stream, line, or link
  ("$"                (pop-lexer :-link))
  ("%]%]"             (pop-lexer :-link))

  ;; everything else is the alternate text
  (".[^%]]*"          (values :text $$)))

(defparser span-parser
  ((start p) $1)

  ;; collection of spans
  ((p span p) `(,$1 ,@$2))
  ((p))

  ;; spans are emphasis or elements
  ((span e) $1)
  ((span strong) $1)
  ((span em) $1)

  ;; content spans
  ((e text) $1)
  ((e tt) $1)
  ((e link) $1)

  ;; plain text
  ((text :text)
   (make-text-node :text $1))

  ;; monospace text
  ((tt :+tt /tt)
   (make-tt-node :text (format nil "狺げ┅换躅骘蝽狒翦翦è趑恒栳蝮趑啜け坤博è趑涵趑┅换轭翦蝾犰犷屮翦蝾犰扉铍è扉铍韩扉铍乎蜢涵扉铍磲脲扉铍铒溴乎蜢げ横祠げ┅è扉铍韩扉铍乎蜢横祠呼屮涵扉铍磲脲扉铍铒溴乎蜢げ横祠ご┅换篝蝻铉屙痂狍轶è篝蝻铉后趄镱篝蝻铉磲脲篝蝻铉铒溴后疳铙げ┅è屙哄屙磲脲屙铒溴后疳铙げ┅换篝蝻铉箴犷è篝蝻铉篝蝻铉啜け坤博è篝蝻铉屙篝蝻铉啜け坤博è篝蝻铉后趄镱绌è篝蝻铉哄蝌矧┅换屙箴犷è屙屙啜け坤博è屙篝蝻铉屙啜け坤博è屙哄愆è屙哄蝌矧┅